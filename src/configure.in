dnl $Id: configure.in,v 1.12 2001/11/05 00:37:34 jnelson Exp $
dnl Process this file with autoconf to produce a configure script.
AC_INIT(boa.c)

dnl Checks for programs.
AC_PROG_LEX
AC_PROG_YACC
AC_PROG_CC
AC_PROG_CPP

dnl Checks for libraries.

dnl Make config.h
AC_CONFIG_HEADER(config.h)
AC_PATH_PROG(GUNZIP, gunzip)
AC_DEFINE_UNQUOTED(GUNZIP, "$ac_cv_path_GUNZIP")

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h unistd.h sys/select.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_HEADER_TIME
AC_STRUCT_TM

AC_PATH_PROG(BC, bc)
AC_PATH_PROG(TR, tr)

if test -z "$ac_cv_path_TR" -o -z "$ac_cv_path_BC"; then
  AC_MSG_WARN(Unable to find either tr or bc -- assuming 32 bits per unsigned long)
else
  AC_MSG_CHECKING(number of places to shift for unsigned long)
  cat >conftest.$ac_ext <<_ACEOF
#include "confdefs.h"
#include <limits.h>

a = ULONG_MAX;
for (i=0;a;i++)
  a=a/2
for(j=0;i;j++) 
  i=i/2
print j-1

_ACEOF
  
  needs_escape_shift=`eval "$ac_cpp conftest.$ac_ext" | $ac_cv_path_TR -d 'UL' | $ac_cv_path_BC`
  rm -f conftest*
  
  if test -n "$needs_escape_shift"; then
    AC_DEFINE_UNQUOTED(NEEDS_ESCAPE_SHIFT, $needs_escape_shift)
    AC_MSG_RESULT($needs_escape_shift)
  fi
fi

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_SETVBUF_REVERSED
AC_CHECK_FUNCS(getcwd gethostname select socket strdup strstr)

test -n "$GCC" && CFLAGS="$CFLAGS -Wall"

AC_OUTPUT(Makefile)
